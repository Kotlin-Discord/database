buildscript {
    repositories {
        maven {
            name = 'KotDis'
            url = 'https://maven.kotlindiscord.com/repository/maven-public/'
        }

        maven {  // Harmonica devs are bad at Maven
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
    }

    dependencies {
        classpath 'org.postgresql:postgresql:42.2.2'
    }
}

plugins {
    id 'application'
    id 'maven-publish'

    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.jakemarsden.git-hooks' version '0.0.1'
    id 'io.gitlab.arturbosch.detekt' version '1.8.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "org.jetbrains.kotlin.kapt" version "1.3.61"
}

group 'com.kotlindiscord.database'
targetCompatibility = 1.11
version '1.0-SNAPSHOT'

repositories {
    maven {
        name = 'KotDis'
        url = 'https://maven.kotlindiscord.com/repository/maven-public/'
    }
}

def exposedVersion = "0.24.1"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.61"

    implementation "org.jetbrains.exposed:exposed-core:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-dao:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposedVersion"

    implementation "org.postgresql:postgresql:42.2.2"

    implementation 'info.picocli:picocli:4.2.0'
    kapt 'info.picocli:picocli-codegen:4.2.0'
}

application {
    mainClassName = "com.kotlindiscord.database.migrator.MigratorKt"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

detekt {
    buildUponDefaultConfig = true
    config = files("detekt.yml")
}

gitHooks {
    hooks = ['pre-commit': 'detekt']
}

jar {
    manifest {
        attributes(
                "Main-Class": "com.kotlindiscord.database.migrator.MigratorKt"
        )
    }
}

publishing {
    repositories {
        maven {
            name = "KotDis"
            url = uri("https://maven.kotlindiscord.com/repository/maven-snapshots/")

            credentials {
                username = project.findProperty("kotdis.user") ?: System.getenv("KOTLIN_DISCORD_USER")
                password = project.findProperty("kotdis.password") ?: System.getenv("KOTLIN_DISCORD_PASSWORD")
            }

            version = project.version
        }
    }

    publications {
        maven(MavenPublication) {
            from(components.java)
            artifacts {
                kotlinSourcesJar
            }
        }
    }
}

kapt {
    arguments {
        arg("project", "${project.group}/${project.name}")
    }
}
tasks.build.dependsOn tasks.shadowJar
